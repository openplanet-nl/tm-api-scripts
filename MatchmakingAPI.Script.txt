/**
 *	API for Matchmaking
 */
#Const Version		"2021-08-02"
#Const ScriptName	"Libs/Nadeo/TMNext/TrackMania/API/MatchmakingAPI.Script.txt"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Libraries
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Include "TextLib" as TL
#Include "Libs/Nadeo/MenuLibs/Common/Manialink/ManiaView2.Script.txt" as MV
#Include "Libs/Nadeo/TMNext/TrackMania/Config.Script.txt" as Config
#Include "Libs/Nadeo/TMNext/TrackMania/Structures/HttpStruct.Script.txt" as HttpStruct
#Include "Libs/Nadeo/CommonLibs/Common/Http.Script.txt" as Http
#Include "Libs/Nadeo/TMNext/TrackMania/Structures/MatchmakingStruct.Script.txt" as MatchmakingStruct
#Include "Libs/Nadeo/CommonLibs/Common/HttpClubAuthorizationHeader.Script.txt" as HttpClubAuthorizationHeader
#Include "Libs/Nadeo/CommonLibs/Common/HttpServerAuthorizationHeader.Script.txt" as HttpServerAuthorizationHeader

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Constants
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
#Const C_Name "component-matchmaking-api" //< Component name
#Const P "ComponentMatchmakingAPI_" //< Prefix use to differentiate functions/variables in the script

#Const C_Headers [
	"Accept" => "application/json",
	"Content-Type" => "application/json"
]

/* Matchmaking API */
#Const C_API_Matchmaking "/api/matchmaking"
#Const C_API_Official "/api/official"
#Const C_API_Match "/api/match"
#Const C_API_Code "/api/code"
#Const C_Route_PostMatchmakingHeartbeat "/:MatchmakingId/heartbeat"
#Const C_Route_GetMatchmakingPlayerStatus "/:MatchmakingId/player-status"
#Const C_Route_PostMatchmakingAccept "/:MatchmakingId/accept"
#Const C_Route_PostMatchmakingCancel "/:MatchmakingId/cancel"
#Const C_Route_GetLeaderboard "/:MatchmakingId/leaderboard"
#Const C_Route_Me "/me"
#Const C_Route_GetParticipantsScores "/:MatchId/participants"
#Const C_Route_GetMatchmakingDivisionRules "/:MatchmakingId/division/display-rules"
#Const C_Route_GetPlayersProgression "/:MatchmakingId/progression/players"
#Const C_Route_GetCode "/:Code"
#Const C_Route_GetSummary "/summary"
#Const C_Route_GetMyStats "/:MatchmakingId/stats/me"

#Const C_RouteParameter_MatchmakingId "MatchmakingId"
#Const C_RouteParameter_MatchId "MatchId"
#Const C_RouteParameter_QueueName "QueueName"
#Const C_RouteParameter_Code "Code"

#Const C_QueryParameter_Players "players"

/* Club API */
#Const C_API_Matches "/api/matches"
#Const C_Route_GetMatchByLid "/:Match"

#Const C_RouteParameter_MatchLid "Match"

/* Competition API */
#Const C_API_SuperRoyal "/api/me/super-royal"
#Const C_Route_Current "/current"
#Const C_Route_Stats "/stats"

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Structures
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/* Matchmaking API */
#Struct ComponentMatchmakingAPI_K_HttpHeartbeatBody {
	Text[] play_with;
	Text code;
}
#Struct ComponentMatchmakingAPI_K_HttpHeartbeat {
	Text status;
	Integer creation_date;
	Integer matchmaking_waiting_time;
	Text match_live_id;
	Integer ban_end_date;
}
#Struct ComponentMatchmakingAPI_K_HttpPenalty {
	Integer creation_date;
	Integer points;
}
#Struct ComponentMatchmakingAPI_K_HttpPlayerStatus {
	ComponentMatchmakingAPI_K_HttpHeartbeat current_heartbeat;
	ComponentMatchmakingAPI_K_HttpPenalty penalty;
	Text current_division;
	Integer current_progression;
	Text matchmaking_status;
}
#Struct ComponentMatchmakingAPI_K_HttpMatch {
	Text join_link;
}
#Struct ComponentMatchmakingAPI_K_HttpPlayerRanking {
	Integer matchmaking_id;
	Text player;
	Integer score;
}
#Struct ComponentMatchmakingAPI_K_HttpParticipant {
	Text player;
	Text current_division;
	Integer current_progression;
}
#Struct ComponentMatchmakingAPI_K_HttpMatchParticipants {
	Boolean is_completed;
	ComponentMatchmakingAPI_K_HttpParticipant[] participants;
}
#Struct ComponentMatchmakingAPI_K_HttpOfficialQueueSummary {
	Integer ranked_3v3_id;
	Integer royal_id;
	Integer super_royal_id;
}
#Struct ComponentMatchmakingAPI_K_HttpDivisionRules {
	Text id;
	Integer position;
	Text display_rule_type;
	Integer display_rule_minimum_points;
	Integer display_rule_maximum_points;
	Integer display_rule_minimum_rank;
}
#Struct ComponentMatchmakingAPI_K_HttpDisplayDivisionRules {
	ComponentMatchmakingAPI_K_HttpDivisionRules[] divisions;
}
#Struct ComponentMatchmakingAPI_K_HttpPlayerProgression {
	Text player;
	Text division;
	Integer progression;
}
#Struct ComponentMatchmakingAPI_K_HttpProgressions {
	ComponentMatchmakingAPI_K_HttpPlayerProgression[] progressions;
}
#Struct ComponentMatchmakingAPI_K_HttpCode {
	Text code;
}
#Struct ComponentMatchmakingAPI_K_HttpSuperRoyal {
	Integer starts_in;
	Text status;
	Text match_live_id;
}
#Struct ComponentMatchmakingAPI_K_HttpSuperRoyalStatistics {
	Integer master_won;
	Integer gold_won;
	Integer silver_won;
	Integer bronze_won;
}
#Struct ComponentMatchmakingAPI_K_HttpPlayerStatistics {
	Integer matchmaking_id;
	Text player;
	Text current_division;
	Integer current_progression;
	Text best_division;
	Integer best_progression;
	Integer best_progression_date;
	Text previous_division;
	Integer previous_progression;
	Integer previous_progression_date;
	Integer mvp_count;
	Integer victories;
	Integer played_matches;
}
#Struct ComponentMatchmakingAPI_K_HttpPlayerLeaderboard {
	Text player;
	Integer rank;
	Integer score;
}
#Struct ComponentMatchmakingAPI_K_HttpLeaderboard {
	Text matchmaking_id;
	Integer cardinal;
	ComponentMatchmakingAPI_K_HttpPlayerLeaderboard[] results;
}
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Functions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
Text[Text] GetHeadersWithClubAuth() {
	declare Text[Text] Headers = C_Headers;
	declare Text AuthorizationHeaders = HttpClubAuthorizationHeader::Get();
	declare Text KeyAutho = TL::SubString(AuthorizationHeaders, 0, TL::Length("Authorization"));
	declare Text ValueAutho = TL::SubString(AuthorizationHeaders, TL::Length("Authorization: "), TL::Length(AuthorizationHeaders));
	Headers[KeyAutho] = ValueAutho;
	return Headers;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Get the players scores (skill level and score delta) for a given match
Http::LibCommonHttp_K_Request Server_GetMatchParticipants(Text _MatchId) {
	declare Text Route = Http::InjectRouteParameters(C_Route_GetParticipantsScores, [C_RouteParameter_MatchId => _MatchId]);

	return Http::CreateGet(Config::Get().APIMatchmakingUrl^C_API_Match^Route, HttpServerAuthorizationHeader::GetHeadersWithClubAuthorization(C_Headers));
}

MatchmakingStruct::LibStructuresMatchmaking_K_MatchParticipants GetResponseFromGetMatchParticipants(Http::LibCommonHttp_K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpMatchParticipants MatchParticipantsResponse;
	MatchParticipantsResponse.fromjson(Http::GetResult(_Request));

	declare MatchmakingStruct::LibStructuresMatchmaking_K_PlayerProgression[Text] Participants;
	foreach (Participant in MatchParticipantsResponse.participants) {
		Participants[Participant.player] = MatchmakingStruct::LibStructuresMatchmaking_K_PlayerProgression {
			AccountId = Participant.player,
			CurrentDivision = Participant.current_division,
			CurrentProgression = Participant.current_progression
		};
	}
	
	declare MatchParticipants = MatchmakingStruct::LibStructuresMatchmaking_K_MatchParticipants {
		IsCompleted = MatchParticipantsResponse.is_completed,
		Participants = Participants
	};

	return MatchParticipants;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the matchmaking division rules
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 										The request
*/
Http::LibCommonHttp_K_Request Server_GetMatchmakingDivisionRules(Text _MatchmakingId) {
	declare Text Route = Http::InjectRouteParameters(
		C_Route_GetMatchmakingDivisionRules,
		[C_RouteParameter_MatchmakingId => _MatchmakingId]
	);
	return Http::CreateGet(Config::Get().APIMatchmakingUrl^C_API_Matchmaking^Route, HttpServerAuthorizationHeader::GetHeadersWithClubAuthorization(C_Headers));
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the matchmaking division rules
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 										The request
*/
Http::LibCommonHttp_K_Request GetMatchmakingDivisionRules(Integer _MatchmakingId) {
	declare Text Route = Http::InjectRouteParameters(
		C_Route_GetMatchmakingDivisionRules,
		[C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);
	return Http::CreateGet(Config::Get().APIMatchmakingUrl^C_API_Matchmaking^Route, GetHeadersWithClubAuth());
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
// Parse the response of Server_GetMatchmakingDivisionRules and GetMatchmakingDivisionRules requests
MatchmakingStruct::LibStructuresMatchmaking_K_Division[Text] GetResponseFromGetMatchmakingDivisionRules(Http::LibCommonHttp_K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpDisplayDivisionRules DisplayDivisionRulesResponse;
	DisplayDivisionRulesResponse.fromjson(Http::GetResult(_Request));

	declare MatchmakingStruct::LibStructuresMatchmaking_K_Division[Text] Divisions;

	foreach (division in DisplayDivisionRulesResponse.divisions) {
		Divisions[division.id] = MatchmakingStruct::LibStructuresMatchmaking_K_Division {
			Id = division.id,
			Position = division.position,
			RuleType = division.display_rule_type,
			MinimumPoints = division.display_rule_minimum_points,
			MaximumPoints = division.display_rule_maximum_points,
			MinimumRank = division.display_rule_minimum_rank
		};
	}

	return Divisions;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the official queue ids summary
	*
	* @return 													The request
*/
Http::LibCommonHttp_K_Request GetOfficialQueueSummary() {
	declare Http::LibCommonHttp_K_Request Request = Http::CreateGet(Config::Get().APIMatchmakingUrl^C_API_Official^C_Route_GetSummary, GetHeadersWithClubAuth());
	return Request;
}

MatchmakingStruct::LibStructuresMatchmaking_K_QueueSummary GetResponseFromGetOfficialQueueSummary(Http::LibCommonHttp_K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpOfficialQueueSummary OfficialQueueSummaryResponse;
	OfficialQueueSummaryResponse.fromjson(Http::GetResult(_Request));

	declare MatchmakingStruct::LibStructuresMatchmaking_K_QueueSummary QueueSummary = MatchmakingStruct::LibStructuresMatchmaking_K_QueueSummary {
		Ranked_3v3_Id = OfficialQueueSummaryResponse.ranked_3v3_id,
		Royal_Id = OfficialQueueSummaryResponse.royal_id,
		Super_Royal_Id = OfficialQueueSummaryResponse.super_royal_id
	};

	return QueueSummary;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the players progressions
	*
	* @param _MatchmakingId				The id of matchmaking queue
	* @param _AccountIds					The array of the players accountIds
	*
	* @return 										The request
*/
Http::LibCommonHttp_K_Request GetMatchmakingPlayersProgressions(Integer _MatchmakingId, Text[] _AccountIds) {
	declare Text Route = Http::InjectRouteParameters(
		C_Route_GetPlayersProgression,
		[C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);

	declare QueryString = Http::CreateQueryString([
		C_QueryParameter_Players => _AccountIds
	]);

	return Http::CreateGet(Config::Get().APIMatchmakingUrl^C_API_Matchmaking^Route^QueryString, GetHeadersWithClubAuth());
}

MatchmakingStruct::LibStructuresMatchmaking_K_PlayerProgression[Text] GetResponseFromGetMatchmakingPlayersProgressions(Http::LibCommonHttp_K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpProgressions ProgressionsResponse;
	ProgressionsResponse.fromjson(Http::GetResult(_Request));

	declare MatchmakingStruct::LibStructuresMatchmaking_K_PlayerProgression[Text] Progressions;
	foreach (Progression in ProgressionsResponse.progressions) {
		Progressions[Progression.player] = MatchmakingStruct::LibStructuresMatchmaking_K_PlayerProgression {
			AccountId = Progression.player,
			CurrentDivision = Progression.division,
			CurrentProgression = Progression.progression
		};
	}

	return Progressions;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the local player leaderboard
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 										The request
*/
Http::LibCommonHttp_K_Request Server_GetMatchmakingLeaderboard(Text _MatchmakingId) {
	declare Text Route = Http::InjectRouteParameters(
		C_Route_GetLeaderboard,
		[C_RouteParameter_MatchmakingId => _MatchmakingId]
	);

	return Http::CreateGet(Config::Get().APIMatchmakingUrl^C_API_Matchmaking^Route, HttpServerAuthorizationHeader::GetHeadersWithClubAuthorization(C_Headers));
}

MatchmakingStruct::LibStructuresMatchmaking_K_PlayerLeaderboard[] GetResponseFromGetMatchmakingLeaderboard(Http::LibCommonHttp_K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpLeaderboard LeaderboardResponse;
	LeaderboardResponse.fromjson(Http::GetResult(_Request));

	declare MatchmakingStruct::LibStructuresMatchmaking_K_PlayerLeaderboard[] Leaderboard = [];
	foreach (Result in LeaderboardResponse.results) {
		Leaderboard.add (MatchmakingStruct::LibStructuresMatchmaking_K_PlayerLeaderboard {
			AccountId = Result.player,
			Rank = Result.rank,
			Score = Result.score
		});
	}

	return Leaderboard;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Get the component
 *
 *	@return														The component id
 */
Text Component() {
	return MV::Component(
C_Name,
"""
""",
"""
#Include "TextLib" as {{{P}}}TL
/* Matchmaking API */
#Const {{{P}}}C_Headers {{{dump(C_Headers)}}}
#Const {{{P}}}C_API_Matchmaking {{{dump(C_API_Matchmaking)}}}
#Const {{{P}}}C_API_Official {{{dump(C_API_Official)}}}
#Const {{{P}}}C_API_Code {{{dump(C_API_Code)}}}
#Const {{{P}}}C_API_SuperRoyal {{{dump(C_API_SuperRoyal)}}}

#Const {{{P}}}C_Route_PostMatchmakingHeartbeat {{{dump(C_Route_PostMatchmakingHeartbeat)}}}
#Const {{{P}}}C_Route_GetMatchmakingPlayerStatus {{{dump(C_Route_GetMatchmakingPlayerStatus)}}}
#Const {{{P}}}C_Route_PostMatchmakingAccept {{{dump(C_Route_PostMatchmakingAccept)}}}
#Const {{{P}}}C_Route_PostMatchmakingCancel {{{dump(C_Route_PostMatchmakingCancel)}}}
#Const {{{P}}}C_Route_GetLeaderboard {{{dump(C_Route_GetLeaderboard)}}}
#Const {{{P}}}C_Route_Me {{{dump(C_Route_Me)}}}
#Const {{{P}}}C_Route_GetCode {{{dump(C_Route_GetCode)}}}
#Const {{{P}}}C_RouteParameter_MatchmakingId {{{dump(C_RouteParameter_MatchmakingId)}}}
#Const {{{P}}}C_Route_GetMatchmakingDivisionRules {{{dump(C_Route_GetMatchmakingDivisionRules)}}}
#Const {{{P}}}C_RouteParameter_QueueName {{{dump(C_RouteParameter_QueueName)}}}
#Const {{{P}}}C_Route_GetPlayersProgression {{{dump(C_Route_GetPlayersProgression)}}}
#Const {{{P}}}C_RouteParameter_Code {{{dump(C_RouteParameter_Code)}}}
#Const {{{P}}}C_Route_GetMyStats {{{dump(C_Route_GetMyStats)}}}
#Const {{{P}}}C_Route_Current {{{dump(C_Route_Current)}}}
#Const {{{P}}}C_Route_Stats {{{dump(C_Route_Stats)}}}

#Const {{{P}}}C_QueryParameter_Players {{{dump(C_QueryParameter_Players)}}}

/* Club API */
#Const {{{P}}}C_API_Matches {{{dump(C_API_Matches)}}}
#Const {{{P}}}C_Route_GetMatchByLid {{{dump(C_Route_GetMatchByLid)}}}
#Const {{{P}}}C_RouteParameter_MatchLid {{{dump(C_RouteParameter_MatchLid)}}}

{{{dumptype(ComponentMatchmakingAPI_K_HttpHeartbeatBody)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpHeartbeat)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpPenalty)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpPlayerStatus)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpMatch)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpPlayerRanking)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpDivisionRules)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpDisplayDivisionRules)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpPlayerProgression)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpProgressions)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpCode)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpSuperRoyal)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpSuperRoyalStatistics)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpPlayerStatistics)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpPlayerLeaderboard)}}}
{{{dumptype(ComponentMatchmakingAPI_K_HttpLeaderboard)}}}
""",
"""
Text[Text] {{{P}}}GetHeadersWithClubAuth() {
	declare Text[Text] Headers = {{{P}}}C_Headers;
	declare Text AuthorizationHeaders = {{{HttpClubAuthorizationHeader::P}}}Get();
	declare Text KeyAutho = {{{P}}}TL::SubString(AuthorizationHeaders, 0, 13);
	declare Text ValueAutho = {{{P}}}TL::SubString(AuthorizationHeaders, 15, {{{P}}}TL::Length(AuthorizationHeaders));
	Headers[KeyAutho] = ValueAutho;
	return Headers;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to post a Matchmaking Heartbeat
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 													The request
*/
{{{Http::P}}}K_Request {{{P}}}PostMatchmakingHeartbeat(Integer _MatchmakingId, Text[] _SquadMembersIds, Text _Code) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_PostMatchmakingHeartbeat,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);
	declare {{{P}}}K_HttpHeartbeatBody HeartBeatBody = {{{P}}}K_HttpHeartbeatBody {
		play_with = _SquadMembersIds,
		code = _Code
	};
	return {{{Http::P}}}CreatePost({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route, HeartBeatBody.tojson(), {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_Heartbeat HeartbeatApiToScript(ComponentMatchmakingAPI_K_HttpHeartbeat _HeartbeatResponse) {
	return {{{MatchmakingStruct::P}}}K_Heartbeat {
		Status = _HeartbeatResponse.status,
		CreationDate = _HeartbeatResponse.creation_date,
		MatchmakingWaitingTime = _HeartbeatResponse.matchmaking_waiting_time,
		MatchLiveId = _HeartbeatResponse.match_live_id,
		BanEndDate = _HeartbeatResponse.ban_end_date
	};
}

{{{MatchmakingStruct::P}}}K_Heartbeat {{{P}}}GetResponseFromPostMatchmakingHeartbeat({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpHeartbeat HeartbeatResponse;
	HeartbeatResponse.fromjson({{{Http::P}}}GetResult(_Request));

	return HeartbeatApiToScript(HeartbeatResponse);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the player status
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 													The request
*/
{{{Http::P}}}K_Request {{{P}}}GetMatchmakingPlayerStatus(Integer _MatchmakingId) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetMatchmakingPlayerStatus,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);
	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_PlayerStatus {{{P}}}GetResponseFromGetMatchmakingPlayerStatus({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpPlayerStatus PlayerStatusResponse;
	PlayerStatusResponse.fromjson({{{Http::P}}}GetResult(_Request));

	return {{{MatchmakingStruct::P}}}K_PlayerStatus {
		CurrentHeartbeat = HeartbeatApiToScript(PlayerStatusResponse.current_heartbeat),
		Penalty = {{{MatchmakingStruct::P}}}K_Penalty {
			CreationDate = PlayerStatusResponse.penalty.creation_date,
			Points = PlayerStatusResponse.penalty.points
		},
		CurrentDivision = PlayerStatusResponse.current_division,
		CurrentProgression = PlayerStatusResponse.current_progression,
		MatchmakingStatus = PlayerStatusResponse.matchmaking_status
	};
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the matchmaking division rules
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}GetMatchmakingDivisionRules(Integer _MatchmakingId) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetMatchmakingDivisionRules,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);
	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_Division[Text] {{{P}}}GetResponseFromGetMatchmakingDivisionRules({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpDisplayDivisionRules DisplayDivisionRulesResponse;
	DisplayDivisionRulesResponse.fromjson({{{Http::P}}}GetResult(_Request));

	declare {{{MatchmakingStruct::P}}}K_Division[Text] Divisions;

	foreach (division in DisplayDivisionRulesResponse.divisions) {
		Divisions[division.id] = {{{MatchmakingStruct::P}}}K_Division {
			Id = division.id,
			Position = division.position,
			RuleType = division.display_rule_type,
			MinimumPoints = division.display_rule_minimum_points,
			MaximumPoints = division.display_rule_maximum_points,
			MinimumRank = division.display_rule_minimum_rank
		};
	}

	return Divisions;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the local player leaderboard
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}GetMyMatchmakingLeaderboard(Integer _MatchmakingId) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetLeaderboard,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);

	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route^{{{P}}}C_Route_Me, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_PlayerRank {{{P}}}GetResponseFromGetMyMatchmakingLeaderboard({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpPlayerRanking LeaderboardResponse;
	LeaderboardResponse.fromjson({{{Http::P}}}GetResult(_Request));

	declare Leaderboard = {{{MatchmakingStruct::P}}}K_PlayerRank {
		MatchmakingId = LeaderboardResponse.matchmaking_id,
		AccountId = LeaderboardResponse.player,
		Score = LeaderboardResponse.score
	};

	return Leaderboard;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the local player leaderboard
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}GetMatchmakingLeaderboard(Integer _MatchmakingId) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetLeaderboard,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);

	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_PlayerLeaderboard[] {{{P}}}GetResponseFromGetMatchmakingLeaderboard({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpLeaderboard LeaderboardResponse;
	LeaderboardResponse.fromjson({{{Http::P}}}GetResult(_Request));

	declare Leaderboard = {{{MatchmakingStruct::P}}}K_PlayerLeaderboard[];
	foreach (Result in LeaderboardResponse.results) {
		Leaderboard.add ({{{MatchmakingStruct::P}}}K_PlayerLeaderboard {
			AccountId = Result.player,
			Rank = Result.rank,
			Score = Result.score
		});
	}

	return Leaderboard;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to post "accept match". NB no response body.
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 													The request
*/
{{{Http::P}}}K_Request {{{P}}}PostMatchmakingAccept(Integer _MatchmakingId) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_PostMatchmakingAccept,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);
	return {{{Http::P}}}CreatePost({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route, "", {{{P}}}GetHeadersWithClubAuth());
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to post "cancel match". NB no response body.
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 													The request
*/
{{{Http::P}}}K_Request {{{P}}}PostMatchmakingCancel(Integer _MatchmakingId) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_PostMatchmakingCancel,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);
	return {{{Http::P}}}CreatePost({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route, "", {{{P}}}GetHeadersWithClubAuth());
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get a match by match Lid
	*
	* @param _MatchId				The Lid of match
	*
	* @return 													The request
*/
{{{Http::P}}}K_Request {{{P}}}GetMatchByLid(Text _MatchLid) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetMatchByLid,
		[{{{P}}}C_RouteParameter_MatchLid => _MatchLid]
	);
	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIBordeauxClubUrl^{{{P}}}C_API_Matches^Route, {{{P}}}GetHeadersWithClubAuth());
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Match by Lid response
	*
	* @param _Request				The request
	*
	* @return 							The match join link
*/
Text {{{P}}}GetResponseFromGetMatchByLid({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpMatch MatchResponse;
	MatchResponse.fromjson({{{Http::P}}}GetResult(_Request));
	return MatchResponse.join_link;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the players progressions
	*
	* @param _MatchmakingId				The id of matchmaking queue
	* @param _AccountIds					The array of the players accountIds
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}GetMatchmakingPlayersProgressions(Integer _MatchmakingId, Text[] _AccountIds) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetPlayersProgression,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);

	declare QueryString ={{{Http::P}}}CreateQueryString([
		{{{P}}}C_QueryParameter_Players => _AccountIds
	]);

	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route^QueryString, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_PlayerProgression[Text] {{{P}}}GetResponseFromGetMatchmakingPlayersProgressions({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpProgressions ProgressionsResponse;
	ProgressionsResponse.fromjson({{{Http::P}}}GetResult(_Request));

	declare {{{MatchmakingStruct::P}}}K_PlayerProgression[Text] Progressions;
	foreach (Progression in ProgressionsResponse.progressions) {
		Progressions[Progression.player] = {{{MatchmakingStruct::P}}}K_PlayerProgression {
			AccountId = Progression.player,
			CurrentDivision = Progression.division,
			CurrentProgression = Progression.progression
		};
	}

	return Progressions;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to generate a Party Code
	*
	* @return 											The request
*/
{{{Http::P}}}K_Request {{{P}}}GeneratePartyCode() {
	return {{{Http::P}}}CreatePost({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Code, "", {{{P}}}GetHeadersWithClubAuth());
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to check if a party code is correct (meaning available in CLUB database)
	*
	* @param _Code								The code to check
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}CheckPartyCode(Text _Code) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetCode,
		[{{{P}}}C_RouteParameter_Code => _Code]
	);
	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Code^Route, {{{P}}}GetHeadersWithClubAuth());
}

Text {{{P}}}GetResponseFromPartyCode({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpCode CodeResponse;
	CodeResponse.fromjson({{{Http::P}}}GetResult(_Request));
	declare Text Code = CodeResponse.code;
	return Code;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to have the SuperRoyal infos
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}GetCurrentSuperRoyal() {
	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APICompetitionUrl^{{{P}}}C_API_SuperRoyal^{{{P}}}C_Route_Current, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_SuperRoyal {{{P}}}GetResponseFromCurrentSuperRoyal({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpSuperRoyal SuperRoyalResponse;
	SuperRoyalResponse.fromjson({{{Http::P}}}GetResult(_Request));

	return {{{MatchmakingStruct::P}}}K_SuperRoyal {
		StartsIn = SuperRoyalResponse.starts_in,
		Status = SuperRoyalResponse.status,
		MatchLiveId = SuperRoyalResponse.match_live_id
	};
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to have the SuperRoyal infos
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}GetSuperRoyalStats() {
	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APICompetitionUrl^{{{P}}}C_API_SuperRoyal^{{{P}}}C_Route_Stats, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_SuperRoyalStatistics {{{P}}}GetResponseFromSuperRoyalStats({{{Http::P}}}K_Request _Request) {
	declare ComponentMatchmakingAPI_K_HttpSuperRoyalStatistics SuperRoyalStatsResponse;
	SuperRoyalStatsResponse.fromjson({{{Http::P}}}GetResult(_Request));

	return {{{MatchmakingStruct::P}}}K_SuperRoyalStatistics {
		BronzeVictories = SuperRoyalStatsResponse.bronze_won,
		SilverVictories = SuperRoyalStatsResponse.silver_won,
		GoldVictories = SuperRoyalStatsResponse.gold_won,
		MasterVictories = SuperRoyalStatsResponse.master_won
	};
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Start a request to get the player statistics in a specific matchmaking
	*
	* @param _MatchmakingId				The id of matchmaking queue
	*
	* @return 										The request
*/
{{{Http::P}}}K_Request {{{P}}}GetPlayerMatchmakingStatistics(Integer _MatchmakingId) {
	declare Text Route = {{{Http::P}}}InjectRouteParameters(
		{{{P}}}C_Route_GetMyStats,
		[{{{P}}}C_RouteParameter_MatchmakingId => ""^_MatchmakingId]
	);

	return {{{Http::P}}}CreateGet({{{Config::P}}}Get().APIMatchmakingUrl^{{{P}}}C_API_Matchmaking^Route, {{{P}}}GetHeadersWithClubAuth());
}

{{{MatchmakingStruct::P}}}K_PlayerStatistics {{{P}}}GetResponseFromGetPlayerMatchmakingStatistics({{{Http::P}}}K_Request _Request) {

	declare ComponentMatchmakingAPI_K_HttpPlayerStatistics PlayerStatisticsResponse;
	PlayerStatisticsResponse.fromjson({{{Http::P}}}GetResult(_Request));

	return {{{MatchmakingStruct::P}}}K_PlayerStatistics {
		MatchmakingId = PlayerStatisticsResponse.matchmaking_id,
		AccountId = PlayerStatisticsResponse.player,
		CurrentDivision = PlayerStatisticsResponse.current_division,
		CurrentProgression = PlayerStatisticsResponse.current_progression,
		BestDivision = PlayerStatisticsResponse.best_division,
		BestProgression = PlayerStatisticsResponse.best_progression,
		BestProgressionDate = PlayerStatisticsResponse.best_progression_date,
		PreviousDivision = PlayerStatisticsResponse.previous_division,
		PreviousProgression = PlayerStatisticsResponse.previous_progression,
		PreviousProgressionDate = PlayerStatisticsResponse.previous_progression_date,
		MVPCount = PlayerStatisticsResponse.mvp_count,
		Victories = PlayerStatisticsResponse.victories,
		PlayedMatches = PlayerStatisticsResponse.played_matches
	};
}
""",
[
	Http::Component(),
	MatchmakingStruct::Component(),
	HttpStruct::Component(),
	Config::Component(),
	HttpClubAuthorizationHeader::Component()
],
[]
);
}
 